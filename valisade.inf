!!! valisade.inf --- Inform 6 source for Valisade 

!! Copyright (C) 2012  David O'Toole

!! Author: David O'Toole <dto@ioforms.org>
!! Keywords: 

!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation, either version 3 of the License, or
!! (at your option) any later version.

!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.

!! You should have received a copy of the GNU General Public License
!! along with this program.  If not, see <http://www.gnu.org/licenses/>.

Constant Story "Valisade";
Constant Headline
    "^An interactive short story
    ^Copyright (C) 2012 by David T. O'Toole^
    ^This program is free software; you can redistribute it and/or modify
    ^it under the terms of the GNU General Public License version 3.^";

Include "Parser";
Include "VerbLib";

!!! Player possessions

Object Bow "bow"
 with 	name 'bow',
	description "An elsewood bow, with a runic inscription of your
 	    family name."
 has	clothing;

Object Quiver "quiver"
 with 	name 'quiver',
	description "The quiver is made of leather, deep brown and
 	    supple.",
 has 	container open clothing;

Class Arrow
 with 	name 'arrow' 'arrows//p',
	article "an",
	plural "arrows",
	description "This is an arrow of blackened mahogany. Its
 	    obsidian head is as sharp as a razor.";

Arrow "arrow" Quiver;
Arrow "arrow" Quiver;
Arrow "arrow" Quiver;
Arrow "arrow" Quiver;

!!! Story objects:

Object Moon "moon"
 has	static scenery
 with 	description "The moon is absolutely full, like a silver Sun.
	    Quine's Crater is visible just below the center.",
	name 'moon' 'luna' 'crater',
	found_in OutsideTent SandyPool ForestPath Clearing SouthernGleyborough;

Object Sun "sun"
 has 	static scenery
 with	description "Sol, the source of all life; the burning spirit 
	    of Sol is the fire that keeps existence alive.";

Object Tent "Leather tent" 
 with 	description
	    "You are sitting in a triangular tent made of Elsewood 
	    stalks and woven leather. The interior of the tent is 
	    warm and dry. The room smells faintly of incense. Some 
	    rounded stones outside the entrance to the tent are just 
	    barely visible by moonlight.",
	name 'tent' 'stalks' 'leather' 'interior' 'stones' 'rounded',
	out_to OutsideTent,
        cant_go "You must go outside of the tent first."
 has 	light static;

Object Hearthstone "Hearthstone" Tent
 with 	name 'hearthstone' 'hearth' 'stone',
	description "This magic stone is about the size of your 
	    fist. A deep orange glow can be seen inside the 
	    quartz-like material, radiating heat and throwing a 
	    modicum of light onto your surroundings. The stone is 
	    always cool to the touch despite its ambient warming 
	    properties.";

Object 	Vademecum "vademecum" Tent
 with 	description
	    "A fine canvas-bound vademecum whose cover bears a 
	    raised semicircle motif pattern, now worn with age. 
	    Every paladin carries one as his commonplace book, to 
	    fill with notes of the day, and to carry letters and 
	    other documents.",
	name 'vademecum' 'vade' 'notebook' 'journal',
 has 	container openable;

Object Itinerary "itinerary" Vademecum
 with 	description
	    "February 11, 8024
	    ^1. Visit Dr. Quine
	    ^2. Meditate
	    ^3. Depart for Valisade
	    ",
        name 'itinerary' 'todo' 'tasks',
 has 	;

Object LetterFromFrancis "letter from Francis" Vademecum
 with   name 'letter' 'francis',
	description 
	    "February 8, 8024^Geoffrey, ^^Though this letter may 
	    find you in good spirits, it brings unpleasant news. 
	    I've just learned that Ian's southern expedition has not 
	    been heard from in twenty days. Imperial ravens have 
	    been searching, but a blizzard has stopped them flying 
	    far south enough to sense Ian's team. You must come back 
	    to Valisade. Please bring a copy of Dr. Quine's 
	    manuscript with you. I'm sorry, brother. ^^Peace be with 
	    you. ^ Francis",
 has	;

Object OutsideTent "Outside of tent"
 has	light
 with 	description
	    "You are standing outside your tent on the riverbank. It 
	    is cold, but not frigid. The moon is full and brilliant 
	    white. The river Ouros is as shallow as a brook here, 
	    and not much wider. The shore continues to the north and 
	    south.",
        in_to Tent,
        s_to "You can't go back home now.",
	n_to SandyPool;

Object Ouros "river Ouros" 
 with 	description "The river appears nearly black. You kneel down
 	    and take a sip of the pure water from your cupped hands.",
	name 'ouros' 'river' 'water' 'brook' 'shore',
	found_in OutsideTent SandyPool,
 has 	scenery;

Object SandyPool "Sandy pool"
 with 	description
	    "Some river water has collected into a small pool here 
	    on the shore, with sand and pebbles. To the northwest, a 
	    path can be seen leading into the heavier brush. Behind 
	    you at a short distance to the south is your tent.",
	name 'path' 'brush' 'tent',
	s_to OutsideTent,
        nw_to ForestPath,
 has 	light;

Object Pool "pool of water" SandyPool
 has 	static scenery
 with 	description "The moon is reflected in the surface of this 
	    little pool.",
	name 'pool' 'water' 'sandy',
	after [;
	 Examine:
	    if (Hematite in SandyPool) 
		print "Something shiny is visible just beneath the 
		    surface of the pool at exactly the spot where 
		    Quine's Crater appears in the reflection; but it 
		    could be an illusion. Looking closer, you 
		    realize there is a polished hematite just 
		    beneath the surface.";
		give Hematite ~concealed;
	];
	    
Object Hematite "glimmering polished hematite" SandyPool
 with 	description
	    "The smooth, mirror-like hematite is about the size of a 
	    grape. It feels unusually heavy.",
	name 'hematite' 'moonstone',
 has	concealed;

Object ForestPath "Forest path"
 with 	description
	    "The sidereal light, heralding sunrise, peeks through the mostly-bare
 	    branches of this forest path. It's wide enough for a small wagon, but
 	    probably too rough. To the southeast you can hear the burbling of the
 	    Ouros; additional forest paths continue to the north and northwest.",
	se_to SandyPool,
	nw_to Clearing,
        n_to SouthernGleyborough,
 has 	light;

Object RoadSign "wooden road sign" ForestPath
 with 	description "The top half's lettering is worn; it says 
	    ~Gleyborough, two miles north~. The bottom half is 
	    illegible.",
        name 'sign' 'wooden',
 has 	static;

Object SouthernGleyborough "Southern Gleyborough"
 with 	description
	    "You are on the outskirts of town. An abandoned house 
	    crumbles about a hundred yards to the west of you; Dr. 
	    Quine's house can be seen at a similar distance to the 
	    east.^There is a light in one of his windows.",
 	w_to "There's nothing in that house.",
	cant_go "The rest of Gleyborough isn't relevant now; you have
 	    to find Dr. Quine.",
 	e_to OutsideQuineHouse,
	s_to ForestPath,
 has 	light;

Object OutsideQuineHouse "Outside Quine's house"
 with 	description
	    "Doctor Quine's house is surprisingly humble for such a 
	    great man. It is painted white, with a red door. The 
	    door is smashed inward, broken nearly in half tallwise, 
	    but something large is blocking the inside of the 
	    doorway and the windows on either side. A circular path 
	    leads either direction around the house to the southeast 
	    and northeast.",
	name 'house' 'door' 'windows',
 	in_to "The door is smashed; you can't get in this way.",
	w_to SouthernGleyborough,
        se_to BehindQuineHouse,
        ne_to BehindQuineHouse,
 has 	light;

Object BehindQuineHouse "Behind Quine's house"
 has 	light,
 with 	in_to "There's no way in from here except down through the
 	    cellar bulkhead door.",
        nw_to OutsideQuineHouse,
        sw__to OutsideQuineHouse,
	name 'house',
	description "You are standing behind Quine's house. The cellar
 	    bulkhead is padlocked.";
 
!! Object CellarDoor

!! Object CellarKey

!! Object QuineHouse "Quine's house"

Object Clearing "A clearing at sunrise"
 with description
	    "The fury of Sol is beginning to light the East. The sky 
	    reddens in suspicion. There is a faint rumour of 
	    patchouli oil. You see a wide circle at the middle of the 
	    clearing where there is no grass.",
 	se_to ForestPath,
 has	light;

Object Circle "circle" Clearing
 with	description
            "The soil in this circular depression is greyish and appears to be 
	    completely dry. There are no footprints anywhere.",
	name 'circle' 'depression' 'middle' 'center' 'grass'
 has 	static concealed;

Object SunIdeogram "sun ideogram" Clearing
 has 	static
 with 	name 'sun' 'ideogram' 'symbol',
	description
	    "A circle with a point at its center is drawn in the 
	    dust at the very center of the depression, as if by a 
	    finger. Someone meditated here.";

Object Raven "black raven"
 has 	animate static
 with	name 'raven' 'black' 'bird' 'courier',
 	description "The raven's feathers are as black as onyx, except for the right wing 
	    which is painted bright red as with all Imperial
 	    couriers. An extraordinary bird, deserving of
 	    veneration.",
	before [;
	 Ask, Tell:
	    print "Upon hearing your voice, The Raven coos and chirps.";
	    move SmallParchment to Clearing;
	    print "The raven chirps happily, and flies away.";
	    give Raven absent;
	    return true;
	 Touch:
	    print "The raven caws and flaps away for a few moments, landing
 	    	again near the ideogram. She seems unsure of who you
 		are.";
 	    return true;
	];

[ DoRavenEncounter;
    print "Your eyes fall upon the ideogram. ^You look upward 
	and suddenly it is as if the sun is at its zenith; the 
	sun is the dot at the center of the ideogram, and the 
	circle is the horizon around your field of view. You are 
	momentarily blinded, but then a feeling of peace ensues. 
	^A calm voice says, ~Look into the eye of the medicine 
	man.~";
    print "^A few moments pass.";
    print "^A large raven appears in the sky above the clearing. It 
	swoops down to land just near the ideogram.^^The raven peers
    	at you, expectantly.";
	
    move Raven to Clearing;
];

Object SmallParchment "small parchment"
 with 	name 'parchment' 'paper',
	description "TODO: distress letter from quine";

!!! Story verbs

[ MeditateSub;
    print "You sit cross-legged on the ground and begin meditating.^";
    if (location == Clearing)
    	DoRavenEncounter();
    return false;
];

!!! Initialisation:

[ Initialise;
    location = Tent;
    lookmode = 2;
    player.description =
	"You wear the rugged leather-and-wool raiment of a Paladin.
	The leather is embossed with runes at its fringes.";
    move Bow to player;
    move Quiver to player;
    give Quiver worn;
];

Include "Grammar";

Verb 'meditate' * -> Meditate;

!!! valisade.inf ends here
