!!! valisade.inf --- Inform 6 source for Valisade 

!! Copyright (C) 2012  David O'Toole

!! Author: David O'Toole <dto@ioforms.org>
!! Keywords: 

!! This program is free software; you can redistribute it and/or modify
!! it under the terms of the GNU General Public License as published by
!! the Free Software Foundation, either version 3 of the License, or
!! (at your option) any later version.

!! This program is distributed in the hope that it will be useful,
!! but WITHOUT ANY WARRANTY; without even the implied warranty of
!! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
!! GNU General Public License for more details.

!! You should have received a copy of the GNU General Public License
!! along with this program.  If not, see <http://www.gnu.org/licenses/>.

!!! Commentary:

!! Play it with Gargoyle: http://ccxvii.net/gargoyle/

!!! Preamble:
   	     
Constant Story "Valisade";
Constant Headline
    "^A visual novella of the forest primeval.
    ^Copyright (C) 2012 by David T. O'Toole^
    ^This program is free software; you can redistribute it and/or modify
    ^it under the terms of the GNU General Public License version 3.^";

Include "Parser";
Include "VerbLib";

!!! Story objects:

Object Tent "Leathern tent"
 with 	description
	    "You are sitting in a triangular tent made of Elsewood 
	    stalks and woven leather. The interior of the tent is 
	    warm and dry, owing to the attuned Hearthstone in the 
	    center where you are warming your hands. The room smells 
	    faintly of incense. The rounded stones outside the 
	    entrance on the river bank are just barely visible by 
	    moonlight.",
	name 'tent' 'stalks' 'leather' 'interior' 'stones' 'rounded',
	out_to OutsideTent,
        cant_go "You must go outside of the tent first."
 has 	light;

Object Vademecum "vademecum" Tent
 with 	description
	    "A fine canvas-bound vademecum whose cover bears a 
	    raised semicircle motif pattern, now worn with age. 
	    Every paladin carries one as his commonplace book, to 
	    fill with notes of the day, and to carry letters and 
	    other documents.",
	name 'vademecum' 'vade' 'notebook' 'journal',
 has 	container openable;

Object Itinerary "itinerary" Vademecum
 with 	description
	    "1. Visit Dr. Quine at the library in Gleyborough
	    ^2. Meditate
	    ^3. Depart for Valisade
	    ",
        name 'itinerary' 'todo' 'tasks',
 has 	;

Object LetterFromFrancis "letter from Francis" Vademecum
 with   name 'letter' 'francis',
	description 
	    "8 February 8024^Geoffrey, ^^Though this letter may find 
	    you in good spirits, it brings unpleasant news. I've 
	    just learned that Ian's southern expedition has not been 
	    heard from in twenty days. Imperial ravens have been 
	    searching, but a cold front has kept them flying far 
	    south enough to sense Ian's team. You must come back to 
	    Valisade. I'm sorry, brother. ^^Peace be with you. ^ 
	    Francis",
 has	;

Object OutsideTent "Outside of tent"
 has	light
 with 	description
	    "You are standing outside your tent on the riverbank. It 
	    is cold, but not frigid. The moon is full and brilliant 
	    white. The river Oulos is as shallow as a brook here, 
	    and not much wider. The shore continues to the north and 
	    south.",
        in_to Tent,
        s_to "You can't go back home now.",
	n_to SandyPool;

Object Oulos "The river Oulos" OutsideTent
 with 	description "The river appears nearly black. You kneel down
 	    and take a sip of the pure water from your cupped hands.",
	name 'oulos' 'river' 'water' 'brook' 'shore',
 has 	concealed;

Object SandyPool "Sandy pool"
 with 	description
	    "Some water has collected into a small pool here, with 
	    sand and pebbles. The moon is reflected on the surface. 
	    To the northwest, a path can be seen leading into the 
	    heavier brush. Behind you at a short distance to the 
	    south is your tent.",
	s_to OutsideTent,
        nw_to ForestPath,
 has 	light;

Object PolishedHematite "glimmering polished hematite" SandyPool
 with 	description
	    "The smooth, mirror-like hematite is about the size of a 
	    grape. It feels unusually heavy.",
	name 'hematite' 'glimmer'
 has 	;

Object ForestPath "Forest path"
 with 	description
	    "The sidereal light heralding sunrise peeks through the mostly-bare
 	    branches of this forest path. It's wide enough for a small wagon, but
 	    probably too rough. To the southeast you can hear the burbling of the
 	    Ouros; the forest path continues to the northwest.",
	se_to SandyPool,
	nw_to Clearing,
 has 	light;

Object Clearing "A clearing at sunrise"
 with description
	    "The fury of Sol is beginning to light the East. The sky 
	    reddens in suspicion. The scent of patchouli. You see a 
	    wide circle at the middle of the clearing where there is 
	    no grass.",
 	se_to ForestPath,
 has	light;

Object Circle "circle" Clearing
 with	description
            "The soil in this circular depression is greyish and appears to be 
	    completely dry. There are no footprints anywhere.",
	name 'circle' 'depression' 'middle' 'center' 'grass'
 has 	concealed;

[ MeditateSub;
    print "You look upward and suddenly it is as if the sun is 
    	at its zenith; the sun is the dot, and the circle is 
    	the horizon around your field of view. You are 
    	momentarily blinded, then a feeling of peace ensues.
    	^A calm voice says, ~Look into the eye of the medicine man.~";
];

Object SunIdeogram "sun ideogram" Clearing
 has 	static
 with 	name 'sun' 'ideogram' 'symbol',
	description
	    "A circle with a point at its center is drawn in the 
	    dust at the very center of the depression, as if by a 
	    finger. Someone meditated here.";

!!! Initialisation:

[ Initialise;
    location = Tent;
];

Include "Grammar";

Verb 'meditate' * -> Meditate;

!!! valisade.inf ends here
